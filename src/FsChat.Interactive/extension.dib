#!meta

{"kernelInfo":{"defaultKernelName":"csharp","items":[{"aliases":[],"name":"csharp"},{"aliases":[],"languageName":"fsharp","name":"fsharp"},{"aliases":["js"],"languageName":"JavaScript","name":"javascript"}]}}

#!fsharp

open System
open System.Threading.Tasks
open Microsoft.DotNet.Interactive
open Microsoft.DotNet.Interactive.Commands
open Microsoft.DotNet.Interactive.Formatting

//do
//    // load jsBundle directly into the notebook
//    // that was an experimnt:
//    //   we now try to avoid loading any JS into the .ipynb file
//    //   instead of this, we programatically load the bundle into js kernel
//    //   then render markdown in js kernel
//    //   and once we have the html, we update the cell display with it.
//    let jsBundle : string = FsChat.Interactive.BundleHelper.loadBundle()
//    $"<script type='module'>{jsBundle}</script>".DisplayAs("text/html") |> ignore

module JsKernelProxy =

    let private jsKernel = Kernel.Root.FindKernelByName("javascript")

    /// append a chunk of text to a markdown cell (everyhting done in js)
    /// only applicable in live streaming mode after jsBundle is loaded
    type AppendMarkdown(tagId:string, text:string) =
        inherit KernelCommand("javascript")
        member _.Text with get () = text
        member _.TagId with get () = tagId
        member this.sendAsync() = jsKernel.SendAsync(this) :> Task

    /// Update a markdown cell with full text:
    /// - send the full text to js kernel
    /// - js kernel will render the markdown
    /// - once rendered, it will send `UpdateDisplayedValue` to the cell in F# kernel
    /// This produces a statically rendered markdown cell (no js needed)
    /// that gets stored into the .ipynb file
    type UpdateFullMarkdown(displayId:string, props:string, css:string, text:string) =
        inherit KernelCommand("javascript")
        member _.Text with get () = text
        member _.DisplayId with get () = displayId
        member _.Css with get () = css
        member _.Props with get () = props
        member this.sendAsync() = jsKernel.SendAsync(this) :> Task

    do
        jsKernel.RegisterCommandType<AppendMarkdown>()
        jsKernel.RegisterCommandType<UpdateFullMarkdown>()
        // load jsBundle into js kernel
        let jsBundle : string = FsChat.Interactive.BundleHelper.loadBundle()
        jsKernel.SendAsync(SubmitCode(jsBundle, "javascript")) |> ignore

(*
    let myKernel =
        let myKernel = Kernel.Root.FindKernelByName("fsharp")
        myKernel.RegisterCommandHandler<AppendData>(fun (command:AppendData) ->
            let js = $"document.getElementById('{command.Id}').innerHTML += `{command.Text}`"
            jsKernel.SendAsync(SubmitCode(js, "javascript")) |> ignore
            Task.CompletedTask)
*)

[<AutoOpen>]
module NotebookRenderer =

    open FSharp.Control
    open FsChat.Types
    //open FsChat.AiApi
    open FsChat.Chat

    /// <summary>An IChatRenderer that formats chat response into html and renders it into a notebook cell.</summary>
    /// <param name="props">Additional html properties to add to target div element, e.g. `"id='myid' class='myclass otherclass'"`</param>
    /// <param name="css">Additional css text, e.g. `"<style>th{color:blue}</style>"` to add before rendered html</param>
    type NotebookRenderer(?props: string, ?css:string) =

        let props = defaultArg props ""
        let css = defaultArg css ""

        let tagIdGenerator =
            let mutable tagId = DateTimeOffset.UtcNow.ToUnixTimeSeconds() - 1727989007L
            fun () -> tagId <- tagId + 1L; tagId

        let formatChunk = function
            | Role role -> sprintf "\nRole: %s\n" role
            | Preamble text
            | Chunk text -> text
            | Finished (reason, stats) -> "" // sprintf "\n\nFinished in %.2fs: %A, %d tokens\n" (float stats.durationMs/1000.0) reason stats.nTokens
            | Err err -> sprintf "\nError: %s\n" err

        let isComplete = function
            | Finished _
            | Err _ -> true
            | _ -> false

        interface IChatRenderer with
            member _.Create() =
                let htmlTagId = $"gptresp{tagIdGenerator()}"
                let display = $"<div class='chat-response' id='{htmlTagId}' {props}/>".DisplayAs("text/html")

                let mutable lastTask = Task.Delay(TimeSpan.FromMilliseconds 250.0)
                let fullText = Text.StringBuilder()
                let mutable pendingTexts = ResizeArray<string>()

                let appendIncremental (stdout:string) =
                    if lastTask.IsCompleted then
                        let stdout =
                            if pendingTexts.Count = 0 then stdout
                            else
                            pendingTexts.Add(stdout)
                            pendingTexts |> String.concat "" |> fun p -> p + stdout
                        pendingTexts.Clear()
                        lastTask <- JsKernelProxy.AppendMarkdown(htmlTagId, stdout).sendAsync()
                    else
                        pendingTexts.Add(stdout)
                        lastTask <- task {
                            do! lastTask
                            match pendingTexts.Count with
                            | 0 ->
                                return! Task.CompletedTask
                            | text ->
                                let text = pendingTexts |> String.concat ""
                                pendingTexts.Clear()
                                return! JsKernelProxy.AppendMarkdown(htmlTagId, text).sendAsync()
                        }

                let renderFinal () =
                    task {
                        do! lastTask
                        do! JsKernelProxy.UpdateFullMarkdown(display.DisplayId, props, css, fullText.ToString()).sendAsync()
                    } |> ignore

                fun (chunk:GptChunk) ->
                    let stdout = formatChunk chunk
                    fullText.Append(stdout) |> ignore
                    if isComplete chunk then
                        renderFinal()
                    else
                        appendIncremental stdout


    /// Formatter for ChatResponse that collapses the details tag
    let chatResponseFormatter =
        Action<_,_>(fun (value:ChatResponse) (writer:TextWriter) ->
            {| text=value.text; result=value.result |} // repack it into anonymous record to avoid recursion
            |> _.ToDisplayString("text/html") // render as html
            |> _.Replace("details open=\"open\"", "details") // but collapse the details tag
            |> writer.Write)

    do
        Chat.defaultRenderer <- NotebookRenderer()
        Formatter.Register<ChatResponse>(chatResponseFormatter, "text/html")

#!javascript

// register the AppendData command handler on javascript kernel
(function() {
    let jskernel = kernel.root.findKernelByName('javascript');

    jskernel.registerCommandHandler({commandType: 'AppendMarkdown', handle: c => {
        const cmd = c.commandEnvelope.command;
        window.appendMdChunk(cmd.tagId, cmd.text);
    }});

    let updateDisplayedValue = (displayId, htmlText) => {
        let fskernel = kernel.root.findKernelByName('fsharp');
        let cmdEnvelope = {
            commandType:'UpdateDisplayedValue',
            command: {
                // KernelCommand  -- polytglot-notebooks/src/contracts.ts
                destinationUri: null,
                originUri: null,
                targetKernelName: fskernel.kernelInfo.localName,
                // UpdateDisplayedValue
                formattedValue: {
                    mimeType: 'text/html',
                    suppressDisplay: false,
                    value: htmlText,
                },
                // id of display value to be updated
                valueId: displayId,
            }
        };
        fskernel.send(cmdEnvelope);
    }

    jskernel.registerCommandHandler({commandType: 'UpdateFullMarkdown', handle: async c => {
        let { displayId, classes, props, css, text } = c.commandEnvelope.command;
        let mdHtml = await window.renderMarkdown(text);
        // remove some spacing to render table rows in one line
        mdHtml = mdHtml.replace(/<\/t(.)>\s+<t/g, '</t$1><t');
        let html = `<div class='chat-response md-blk'>${css}<div ${props}>${mdHtml}</div></div>`;
        updateDisplayedValue(displayId, html);
        //updateDisplayedValue(displayId, text);
    }});
    console.log("FsChat is ready.");
})();
